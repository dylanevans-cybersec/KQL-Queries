// detect command shell execution from popular browser executables

let browserProcesses = dynamic(["chrome.exe", "firefox.exe", "msedge.exe", "iexplore.exe"]); //expand on this with additional executable names if required
let shellProcesses = dynamic(["cmd.exe", "powershell.exe"]);
// Detecting shell processes spawned
let browserSpawningShell = DeviceProcessEvents
| where InitiatingProcessFileName in (browserProcesses) and FileName in (shellProcesses)
| project BrowserProcessId = InitiatingProcessId, ShellProcessId = ProcessId, DeviceId, TimeGenerated;
// Detecting network activity from the shell
let shellNetworkActivity = DeviceNetworkEvents
| where InitiatingProcessId in ((browserSpawningShell | project ShellProcessId))
| project ShellProcessId = InitiatingProcessId, RemoteUrl, TimeGenerated;
// Correlate all the events
browserSpawningShell
| join kind=inner (shellNetworkActivity) on ShellProcessId
| project TimeGenerated, DeviceId, BrowserProcessId, ShellProcessId, RemoteUrl
